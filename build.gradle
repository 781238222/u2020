buildscript {
  ext.versions = [
      'compileSdk': 26,
      'buildTools': '26.0.0',
      'minSdk': 15,
      'targetSdk': 25,

      'supportLibrary': '26.0.0-beta2',
      'dagger': '1.2.5',
      'okhttp': '3.6.0',
      'retrofit': '2.2.0',
      'butterKnife': '8.5.1',
      'leakCanary': '1.5',
      'espresso': '2.2.2',
      'astl': '0.5',
  ]

  ext.deps = [
      'support': [
          'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
          'v4': "com.android.support:support-v4:${versions.supportLibrary}",
          'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
          'design': "com.android.support:design:${versions.supportLibrary}",
          'recyclerView': "com.android.support:recyclerview-v7:${versions.supportLibrary}",
      ],
      'dagger': [
          'runtime': "com.squareup.dagger:dagger:${versions.dagger}",
          'compiler': "com.squareup.dagger:dagger-compiler:${versions.dagger}",
      ],
      'okhttp': [
          'core': "com.squareup.okhttp3:okhttp:${versions.okhttp}",
          'logger': "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}",
      ],
      'okio': 'com.squareup.okio:okio:1.11.0',
      'picasso': 'com.squareup.picasso:picasso:2.5.2',
      'picassoOkHttpDownloader': 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0',
      'retrofit': [
          'core': "com.squareup.retrofit2:retrofit:${versions.retrofit}",
          'mock': "com.squareup.retrofit2:retrofit-mock:${versions.retrofit}",
          'moshi': "com.squareup.retrofit2:converter-moshi:${versions.retrofit}",
          'rxjava': "com.squareup.retrofit2:adapter-rxjava:${versions.retrofit}",
      ],
      'moshi': 'com.squareup.moshi:moshi:1.4.0',
      'butterKnife': [
          'runtime': "com.jakewharton:butterknife:${versions.butterKnife}",
          'compiler': "com.jakewharton:butterknife-compiler:${versions.butterKnife}",
      ],
      'timber': 'com.jakewharton.timber:timber:4.5.1',
      'byteUnits': 'com.jakewharton.byteunits:byteunits:0.9.1',
      'rxbinding': 'com.jakewharton.rxbinding:rxbinding:1.0.1',
      'madge': 'com.jakewharton.madge:madge:1.1.4',
      'scalpel': 'com.jakewharton.scalpel:scalpel:1.1.2',
      'processPhoenix': 'com.jakewharton:process-phoenix:1.1.1',
      'leakCanary': [
          'real': "com.squareup.leakcanary:leakcanary-android:${versions.leakCanary}",
          'noOp': "com.squareup.leakcanary:leakcanary-android-no-op:${versions.leakCanary}",
      ],
      'rx': [
          'core': 'io.reactivex:rxjava:1.2.4',
          'android': 'io.reactivex:rxandroid:1.2.1',
          'preferences': 'com.f2prateek.rx.preferences:rx-preferences:1.0.2',
      ],
      'threeTenAbp': 'com.jakewharton.threetenabp:threetenabp:1.0.4',
      'telescope': 'com.mattprecious.telescope:telescope:2.1.0',
      'junit': 'junit:junit:4.12',
      'espresso': [
          'core': "com.android.support.test.espresso:espresso-core:${versions.espresso}",
          'contrib': "com.android.support.test.espresso:espresso-contrib:${versions.espresso}",
      ],
      'astl': [
          'runner': "com.android.support.test:runner:${versions.astl}",
          'rules': "com.android.support.test:rules:${versions.astl}",
      ],
      'spoon': 'com.squareup.spoon:spoon-client:1.7.0',
      'truth': 'com.google.truth:truth:0.30',
  ]

  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  dependencies {
    classpath 'com.android.tools.build:gradle:3.0.0-alpha7'
    classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
  }
}

subprojects {
  repositories {
    google()
    mavenCentral()
    jcenter()
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all of the primary support libraries to use the same version.
        if (details.requested.group == 'com.android.support') {
          details.useVersion versions.supportLibrary
        }
      }
    }
  }
}
